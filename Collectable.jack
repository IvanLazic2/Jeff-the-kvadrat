// 0 - coin
// 1 - life

class Collectable
{
    field int X;
    field int Y;
    field int Type;
    field int memAddress;

    field bool IsAvailable;

    constructor Collectable new(int x, int y, int type)
    {
        let X = x;
        let Y = y;
        let memAddress = Constants.getMemAddress(x, y);
        let Type = type;

        let IsAvailable = true;

        return this;
    }

    method int getX() { return X; }
    method int getY() { return Y; }
    method int getType() { return Type; }
    method bool getIsAvailable() { return IsAvailable; }
    
    method void setIsAvailable(int b) { let IsAvailable = b; return; }

    method int CheckCollisionWithCharacter(int cx, int cy, int cw, int ch)
    {
        if (IsAvailable & Collision.IsBulletCollidedWithEntity(cx, cy, cw, ch, X, Y))
        {
            let IsAvailable = false;
            return Type;
        }

        return -1;
    }

    method void Collect()
    {
        let IsAvailable = false;
        return;
    }

    method void ScrollRight()
	{
        if (IsAvailable)
        {
            do Clear();
            let memAddress = memAddress + 1;
            do Draw(true);
        }

        return;
	}

	method void ScrollLeft()
    {
        if (IsAvailable)
        {
            do Clear();
            let memAddress = memAddress - 1;
            do Draw(true);
        }

		return;
    }

    method void Clear()
	{
		do Draw(false);
		return;
	}

    method void Draw(bool display)
    {
        // COIN
		if (Type = 0)
		{
			do Coin.DrawFrame(memAddress, display, false, X, Y);
		}
        // LIFE
		else { if (Type = 1)
		{
			do Heart.DrawFrame(memAddress, display, false, X, Y);
		}}
		

		return;
    }
}